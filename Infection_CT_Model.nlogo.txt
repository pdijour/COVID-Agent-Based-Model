globals
[
  max-infected
  regular-timer       ;; number of ticks since beginning of simulation
]

turtles-own
[
  infected?           ;; if true, the turtle is infectious, can be either symptomatic or asymptomatic
  isolated?           ;; if true, the turtle can't be infected
  quarantined?        ;; if true, the turtle can't be infected
  resistant?          ;; if true, the turtle can't be infected
  dead?               ;; if true, the turtle can't be infected
  recovery-timer      ;; number of ticks since infection began
  isolation-timer     ;; number of ticks since isolation began
  incubation-timer    ;; number of ticks to begin quarantine when infected
]

to setup
  clear-all
  setup-nodes
  setup-spatially-clustered-network
  ask n-of initial-outbreak-size turtles
    [ become-infected ]
  ask links [ set color white ]
  set max-infected (count turtles with [infected?])
  reset-ticks
end

to setup-nodes
  set-default-shape turtles "circle"
  create-turtles number-of-nodes
  [
    ; for visual reasons, we don't put any nodes *too* close to the edges
    setxy (random-xcor * 0.95) (random-ycor * 0.95)
    become-susceptible
;;    set virus-check-timer random virus-check-frequency
    set regular-timer 0
    set recovery-timer random max-recovery-time
    set isolation-timer 1
    set incubation-timer random max-incubation-time
  ]
end

to setup-spatially-clustered-network
  let num-links (average-node-degree * number-of-nodes) / 2
  while [count links < num-links ]
  [
    ask one-of turtles
    [
      let choice (min-one-of (other turtles with [not link-neighbor? myself])
                   [distance myself])
      if choice != nobody [ create-link-with choice ]
    ]
  ]
  ; make the network look a little prettier
  repeat 10
  [
    layout-spring turtles links 0.3 (world-width / (sqrt number-of-nodes)) 1
  ]
end

to go
  set regular-timer regular-timer + 1

  if all? turtles [not (infected? or isolated? or quarantined?)]
    [ stop ]

  ask turtles with [infected? or quarantined?]
    [ set recovery-timer recovery-timer + 1
    if recovery-timer >= max-recovery-time
      [ set recovery-timer 0 ] ]

  ask turtles with [isolated?]
    [ set isolation-timer isolation-timer + 1
    if isolation-timer >= isolation-time
      [ set isolation-timer 0 ] ]

  ask turtles with [infected?]
    [ set incubation-timer incubation-timer + 1
    if incubation-timer >= max-incubation-time
      [ set incubation-timer 0 ] ]

  spread-virus-during-incubation
  spread-virus-after-incubation
  isolate-susceptible
  isolate-at-a-distance
  end-isolation
  quarantine-symptomatic
  quarantine-asymptomatic
  quarantine-asymptomatic-at-a-distance
  recover-or-die
  calculate-max-infected
  tick
end

to become-infected  ;; turtle procedure
  set infected? true
  set isolated? false
  set quarantined? false
  set resistant? false
  set dead? false
  set color red
end

to become-isolated  ;; turtle procedure
  set infected? false
  set isolated? true
  set quarantined? false
  set resistant? false
  set dead? false
  set color pink
end

to become-susceptible  ;; turtle procedure
  set infected? false
  set isolated? false
  set quarantined? false
  set resistant? false
  set dead? false
  set color blue
end

to become-quarantined  ;; turtle procedure
  set infected? false
  set isolated? false
  set quarantined? true
  set resistant? false
  set dead? false
  set color orange
end

to become-resistant  ;; turtle procedure
  set infected? false
  set isolated? false
  set quarantined? false
  set resistant? true
  set dead? false
  set color gray
  ask my-links [ set color gray - 2 ]
end

to become-dead  ;; turtle procedure
  set infected? false
  set isolated? false
  set quarantined? false
  set resistant? true
  set dead? true
  set color black
  ask my-links [ set color gray - 2 ]
end

;; Assumes spread during incubation period because turtle spreads infection and does not quarantine until it is symptomatic
to spread-virus-during-incubation
  if spread-during-incubation
    [ ask turtles with [infected?]
      [ ask link-neighbors with [not (resistant? or quarantined? or dead? or isolated?)]
          [ if random-float 1 < infection-rate
            [ if random 100 >= using-preventative-CT
              [ become-infected ] ] ] ] ] ;; primary susceptible contacts without the app or before the infected turtle is syptomatic don't isolate, and thus become infected
end

to spread-virus-after-incubation
  if not spread-during-incubation
    [ask turtles with [infected? and incubation-timer = 0]
      [ ask link-neighbors with [not (resistant? or quarantined? or dead? or isolated?)]
        [ if random-float 1 < infection-rate
          [ if random 100 >= using-preventative-CT
            [ become-infected ] ] ] ] ] ;; primary susceptible contacts without the app or before the infected turtle is syptomatic don't isolate, and thus become infected
end

to isolate-susceptible
  ask turtles with [infected? and incubation-timer = 0] ;; infected turtle's symptoms are showing and turtle has notified app
    [ ask link-neighbors with [not (resistant? or quarantined? or dead? or isolated?)]
        [ if random-float 1 < infection-rate
          [ if random 100 < using-preventative-CT
            [ become-isolated ] ] ] ]  ;; primary susceptible contacts with the preventative app isolate
end

to isolate-at-a-distance
  ask turtles with [infected? and incubation-timer = 0] ;; infected turtle's symptoms are showing and turtle has notified app
    [ ask link-neighbors with [not (resistant? or quarantined? or dead? or isolated?)]
      [ ask link-neighbors with [not (infected? or resistant? or quarantined? or dead? or isolated?)]
        [ if  random 100 < using-preventative-CT and isolate-second-in-link
          [ become-isolated ] ] ] ]   ;; secondary susceptible contacts with the preventative app isolate
end

to end-isolation
  ask turtles with [(isolated?) and isolation-timer = 0]
    [ become-susceptible ]  ;; isolated turtles re-enter the susceptible population when they are done isolating
end

to quarantine-symptomatic
  ask turtles with [infected? and incubation-timer = 0]  ;; infected turtle's symptoms are showing
    [ if  random 100 >= percent-asymptomatic
      [ become-quarantined ] ]  ;; symptomatic turtle quarantines
end

to quarantine-asymptomatic
  ask turtles with [infected? and incubation-timer = 0]  ;; infected turtle's symptoms are showing
    [ ask link-neighbors with [infected?]  ;; infected turtle's infected neighbor
      [ if  random 100 < percent-asymptomatic and random 100 < using-reactive-CT
        [ become-quarantined ] ] ]  ;; infectious asymptomatic primary contact turtles with the reactive app quarantine
end

to quarantine-asymptomatic-at-a-distance
  ask turtles with [infected? and incubation-timer = 0]  ;; infected turtle's symptoms are showing
    [ ask link-neighbors with [infected?]  ;; infected turtle's infected neighbor
      [ ask link-neighbors with [infected?]  ;; infected turtle's infected neighbor
        [ if  random 100 < percent-asymptomatic and random 100 < using-reactive-CT and quarantine-second-in-link
          [ become-quarantined ] ] ] ]
end

to recover-or-die
  ask turtles with [(infected? or quarantined?) and recovery-timer = 0]
    [ ifelse random 100 >= death-chance
      [ become-resistant]
      [ become-dead ] ]
end

to calculate-max-infected
  let x (count turtles with [infected?])
  if x > max-infected
  [set max-infected x]
end

to-report max-infected-prop
  report max-infected / number-of-nodes
end

to-report prop-uninfected
  report (count turtles with [not infected? and not resistant? and not dead?]) / number-of-nodes
end

to-report prop-dead
  report (count turtles with [dead?]) / number-of-nodes
end

to-report R-naught
  report (infection-rate / (1 / max-recovery-time))
end

; Copyright 2008 Uri Wilensky. Modified by Phoebe Dijour to include predictive and reactive contact tracing intervention
@#$#@#$#@
GRAPHICS-WINDOW
265
10
663
409
-1
-1
9.5122
1
10
1
1
1
0
0
0
1
-20
20
-20
20
1
1
1
ticks
30.0

SLIDER
24
211
229
244
infection-rate
infection-rate
0.0
1
0.2
0.1
1
days^-1
HORIZONTAL

BUTTON
25
125
120
165
NIL
setup
NIL
1
T
OBSERVER
NIL
NIL
NIL
NIL
1

BUTTON
135
125
230
165
NIL
go
T
1
T
OBSERVER
NIL
NIL
NIL
NIL
0

PLOT
265
422
586
617
Network Status
time
% of nodes
0.0
50.0
0.0
100.0
true
true
"" ""
PENS
"susceptible" 1.0 0 -13345367 true "" "plot (count turtles with [not infected? and not resistant?]) / (count turtles) * 100"
"infected" 1.0 0 -2674135 true "" "plot (count turtles with [infected?]) / (count turtles) * 100"
"recovered" 1.0 0 -7500403 true "" "plot (count turtles with [resistant?]) / (count turtles) * 100"
"dead" 1.0 0 -16777216 true "" "plot (count turtles with [dead?]) / (count turtles) * 100"
"quarantined" 1.0 0 -955883 true "plot (count turtles with [dead?]) / (count turtles) * 100" "plot (count turtles with [dead?]) / (count turtles) * 100"
"isolated" 1.0 0 -1664597 true "" "plot (count turtles with [dead?]) / (count turtles) * 100"

SLIDER
25
15
230
48
number-of-nodes
number-of-nodes
10
300
200.0
5
1
NIL
HORIZONTAL

SLIDER
21
441
230
474
using-reactive-CT
using-reactive-CT
0
100
0.0
1
1
%
HORIZONTAL

SLIDER
25
85
230
118
initial-outbreak-size
initial-outbreak-size
1
number-of-nodes
1.0
1
1
NIL
HORIZONTAL

SLIDER
25
50
230
83
average-node-degree
average-node-degree
1
number-of-nodes - 1
10.0
1
1
NIL
HORIZONTAL

SLIDER
23
365
229
398
death-chance
death-chance
0
100
7.0
1
1
%
HORIZONTAL

SLIDER
23
327
228
360
max-recovery-time
max-recovery-time
0
30
14.0
1
1
ticks
HORIZONTAL

MONITOR
591
421
712
466
NIL
max-infected-prop
17
1
11

MONITOR
591
470
713
515
NIL
prop-uninfected
17
1
11

MONITOR
591
519
712
564
NIL
prop-dead
17
1
11

MONITOR
592
570
712
615
NIL
R-naught
17
1
11

SLIDER
20
522
231
555
using-preventative-CT
using-preventative-CT
0
100
0.0
1
1
%
HORIZONTAL

SWITCH
20
596
231
629
isolate-second-in-link
isolate-second-in-link
1
1
-1000

SLIDER
20
559
231
592
isolation-time
isolation-time
0
20
14.0
1
1
ticks
HORIZONTAL

SLIDER
24
173
229
206
percent-asymptomatic
percent-asymptomatic
0
100
50.0
1
1
%
HORIZONTAL

TEXTBOX
23
424
239
442
Contact Tracing Interventions:
11
0.0
1

SWITCH
21
477
231
510
quarantine-second-in-link
quarantine-second-in-link
1
1
-1000

SWITCH
24
289
229
322
spread-during-incubation
spread-during-incubation
0
1
-1000

SLIDER
24
249
229
282
max-incubation-time
max-incubation-time
0
20
7.0
1
1
ticks
HORIZONTAL

@#$#@#$#@
## WHAT IS IT?

This model demonstrates the spread of a virus through a network with options to visualize the effects of app-based contact tracing interventions. It is based on an SIRD epidemic model, where individuals can be grouped into susceptible, infected, and recovered, or dead. This model also allows for a certain percentage of the population to be asymptomatic yet infectious and for individuals to be infectious during the viral incubation period.

## HOW IT WORKS

During each time step (tick), each infected individual (colored red) attempts to infect all of its direct neighbors.  Susceptible neighbors (colored blue) will be infected with a probability given by the INFECTION-RATE slider. Resistant individual (colored gray) and dead individuals (colored black) cannot be infected.

During the incubation period (when an individual is infected but not experiencing symptoms, and therefore not quarantined, recovered, or dead), the virus can either be infectious or not infectious, set by the SPREAD-DURING-INCUBATION switch.

All asymptomatic individuals, determined by the PERCENT-ASYMPTOMATIC slider, can recover (after a certain amount of time set by the MAX-RECOVERY-TIME slider) or die (determined by the DEATH-CHANCE slider). All symptomatic individuals enter quarantine once their incubation time is over, determined by the MAX-INCUBATION-TIME slider, and then either recover or die.

When a node becomes resistant or dead, the links between it and its neighbors are darkened, since they are no longer possible vectors for spreading the virus.

## HOW TO USE IT

Using the sliders, choose the NUMBER-OF-NODES and the AVERAGE-NODE-DEGREE (average number of links coming out of each node).

The network that is created is based on proximity (Euclidean distance) between nodes.  A node is randomly chosen and connected to the nearest node that it is not already connected to.  This process is repeated until the network has the correct number of links to give the specified average node degree.

The INITIAL-OUTBREAK-SIZE slider determines how many of the nodes will start the simulation infected with the virus.

Then press SETUP to create the network.  Press GO to run the model.  The model will stop running once the virus has completely died out.

General viral parameters, including the PERCENT-ASYMPTOMATIC, INFECTION-RATE, MAX-INCUBATION-TIME, SPREAD-DURING-INCUBATION (a binary value), MAX-RECOVERY-TIME, and DEATH-CHANCE can be adjusted before pressing GO, or while the model is running.

The NETWORK STATUS plot shows the number of nodes in each state (S, I, R, D, Q, I) over time.

## THINGS TO NOTICE

At the end of the run, some nodes are still susceptible, while others have become immune. No nodes are still in quarantine or isolated. The maximum proportions of the population that was infected, the proportion of the population left uninfected, the proportion of the population dead are all displayed next to the NETWORK STATUS plot. The R0 (R-naught) value, which is a measure of how contagious a virus is, calculated by dividing rate of infection by rate of recovery, is also displayed. When R0 is below the value of one, an epidemic cannot be sustained, while an R0 greater than one indicates that an epidemic is possible, and an R0 value much greater than one indicates that an epidemic is certain. A larger R0 indicates a larger epidemic risk and generates a “higher curve,” where more individuals are infected at once, but the epidemic lasts for a shorter amount of time. The concept of “flattening the curve” requires a lower R0 value and prolongs the timeframe of the virus but ensures that healthcare facilities can handle the influx of patients at the peak of infection. The R0 value displayed does not change throughout the course of viral spread.

## THINGS TO TRY

All of the sliders and switches in the top half of the interface can be changed to see the effects of different viral parameters on viral spread. All of the sliders and switches in the bottom half of the interface can be changed to see the effects of app-based contact tracing on viral spread. Two different forms of app-based contact tracing can be modeled: reactive (infectious asymptomatic indiviuals quarantine after learning via an app that they have come in contact with an infected individual), and preventative (susceptible individuals isolate before they become infected to prevent contracting the virus). A certain percentage of the population will have and comply to these apps, based on the USING-REACTIVE-CT and USING-PREVENTATIVE-CT sliders.

Each of these interventions can be set to affect the SECOND-IN-LINK or not. For instance, if QUARANTINE-SECOND-IN-LINK is on, individuals that are not directly linked to an infected individual, but secondarily linked, have a certain probability, based on USING-REACTIVE-CT to have the reactive contact tracing app and quarantine once infetious but asymptomatic. You can adjust any of these sliders and switches to see a wide array  of virus parameters, interventions, and outcomes.

## EXTENDING THE MODEL

This model only has the option to isolate and quarantine secondary contacts. You could change this model to include tertiary, quaternary, and further isolation/quarantine interventions. Other interventions, such as hand-washing and the use of PPE including gloves and masks, can be modeled simultaneously.

This model could be changed to use directed links instead of undirected links in order to account for asymmetric relationships.

Multiple viruses could be modeled at the same time.

The model could be modified to have a mutating virus that can infect individuals immune only to the original virus.

Age and location structures could be added to the model. For instance, individuals who are over the age of 60 are more likely to die from SARS-CoV-2 than are younger individuals, and more densely population locations are likely to have a higher rate of infection. Locations such as hospitals, nursing homes, and schools can also have higher rate of infections for certain viruses.

## RELATED MODELS

Virus, Disease, Preferential Attachment, Diffusion on a Directed Network

## NETLOGO FEATURES

Links are used for modeling the network.  The `layout-spring` primitive is used to position the nodes and links such that the structure of the network is visually clear.

Though it is not used in this model, there exists a network extension for NetLogo that you can download at: https://github.com/NetLogo/NW-Extension.

## HOW TO CITE

This model is based on a basic virus-spread model made by Stonedalh and Wilensjy at the Northwestern University Center for Connected Learning and Computer-Based Modeling, cited below. If you mention this model or the NetLogo software in a publication, they ask that you include the citations below. The model has been expanded to include many additional features by me, Phoebe Dijour, a Biomedical Engineering student at Duke University. I ask that if you use this model or software, you cite it as written below.

Please cite both the original and current authors of the model as:

* Dijour, P. (2020). NetLogo Virus on a Network with App-Based Contact Tracing Interventions. Pratt School of Engineering, Duke University, Durham, NC.

* Stonedahl, F. and Wilensky, U. (2008).  NetLogo Virus on a Network model.  http://ccl.northwestern.edu/netlogo/models/VirusonaNetwork.  Center for Connected Learning and Computer-Based Modeling, Northwestern University, Evanston, IL.

Please cite the NetLogo software as:

* Wilensky, U. (1999). NetLogo. http://ccl.northwestern.edu/netlogo/. Center for Connected Learning and Computer-Based Modeling, Northwestern University, Evanston, IL.

## COPYRIGHT AND LICENSE

Copyright 2008 Uri Wilensky.

![CC BY-NC-SA 3.0](http://ccl.northwestern.edu/images/creativecommons/byncsa.png)

This work is licensed under the Creative Commons Attribution-NonCommercial-ShareAlike 3.0 License.  To view a copy of this license, visit https://creativecommons.org/licenses/by-nc-sa/3.0/ or send a letter to Creative Commons, 559 Nathan Abbott Way, Stanford, California 94305, USA.

Commercial licenses are also available. To inquire about commercial licenses, please contact Uri Wilensky at uri@northwestern.edu.

<!-- 2008 Cite: Stonedahl, F. -->
@#$#@#$#@
default
true
0
Polygon -7500403 true true 150 5 40 250 150 205 260 250

airplane
true
0
Polygon -7500403 true true 150 0 135 15 120 60 120 105 15 165 15 195 120 180 135 240 105 270 120 285 150 270 180 285 210 270 165 240 180 180 285 195 285 165 180 105 180 60 165 15

arrow
true
0
Polygon -7500403 true true 150 0 0 150 105 150 105 293 195 293 195 150 300 150

box
false
0
Polygon -7500403 true true 150 285 285 225 285 75 150 135
Polygon -7500403 true true 150 135 15 75 150 15 285 75
Polygon -7500403 true true 15 75 15 225 150 285 150 135
Line -16777216 false 150 285 150 135
Line -16777216 false 150 135 15 75
Line -16777216 false 150 135 285 75

bug
true
0
Circle -7500403 true true 96 182 108
Circle -7500403 true true 110 127 80
Circle -7500403 true true 110 75 80
Line -7500403 true 150 100 80 30
Line -7500403 true 150 100 220 30

butterfly
true
0
Polygon -7500403 true true 150 165 209 199 225 225 225 255 195 270 165 255 150 240
Polygon -7500403 true true 150 165 89 198 75 225 75 255 105 270 135 255 150 240
Polygon -7500403 true true 139 148 100 105 55 90 25 90 10 105 10 135 25 180 40 195 85 194 139 163
Polygon -7500403 true true 162 150 200 105 245 90 275 90 290 105 290 135 275 180 260 195 215 195 162 165
Polygon -16777216 true false 150 255 135 225 120 150 135 120 150 105 165 120 180 150 165 225
Circle -16777216 true false 135 90 30
Line -16777216 false 150 105 195 60
Line -16777216 false 150 105 105 60

car
false
0
Polygon -7500403 true true 300 180 279 164 261 144 240 135 226 132 213 106 203 84 185 63 159 50 135 50 75 60 0 150 0 165 0 225 300 225 300 180
Circle -16777216 true false 180 180 90
Circle -16777216 true false 30 180 90
Polygon -16777216 true false 162 80 132 78 134 135 209 135 194 105 189 96 180 89
Circle -7500403 true true 47 195 58
Circle -7500403 true true 195 195 58

circle
false
0
Circle -7500403 true true 0 0 300

circle 2
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240

cow
false
0
Polygon -7500403 true true 200 193 197 249 179 249 177 196 166 187 140 189 93 191 78 179 72 211 49 209 48 181 37 149 25 120 25 89 45 72 103 84 179 75 198 76 252 64 272 81 293 103 285 121 255 121 242 118 224 167
Polygon -7500403 true true 73 210 86 251 62 249 48 208
Polygon -7500403 true true 25 114 16 195 9 204 23 213 25 200 39 123

cylinder
false
0
Circle -7500403 true true 0 0 300

dot
false
0
Circle -7500403 true true 90 90 120

face happy
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 255 90 239 62 213 47 191 67 179 90 203 109 218 150 225 192 218 210 203 227 181 251 194 236 217 212 240

face neutral
false
0
Circle -7500403 true true 8 7 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Rectangle -16777216 true false 60 195 240 225

face sad
false
0
Circle -7500403 true true 8 8 285
Circle -16777216 true false 60 75 60
Circle -16777216 true false 180 75 60
Polygon -16777216 true false 150 168 90 184 62 210 47 232 67 244 90 220 109 205 150 198 192 205 210 220 227 242 251 229 236 206 212 183

fish
false
0
Polygon -1 true false 44 131 21 87 15 86 0 120 15 150 0 180 13 214 20 212 45 166
Polygon -1 true false 135 195 119 235 95 218 76 210 46 204 60 165
Polygon -1 true false 75 45 83 77 71 103 86 114 166 78 135 60
Polygon -7500403 true true 30 136 151 77 226 81 280 119 292 146 292 160 287 170 270 195 195 210 151 212 30 166
Circle -16777216 true false 215 106 30

flag
false
0
Rectangle -7500403 true true 60 15 75 300
Polygon -7500403 true true 90 150 270 90 90 30
Line -7500403 true 75 135 90 135
Line -7500403 true 75 45 90 45

flower
false
0
Polygon -10899396 true false 135 120 165 165 180 210 180 240 150 300 165 300 195 240 195 195 165 135
Circle -7500403 true true 85 132 38
Circle -7500403 true true 130 147 38
Circle -7500403 true true 192 85 38
Circle -7500403 true true 85 40 38
Circle -7500403 true true 177 40 38
Circle -7500403 true true 177 132 38
Circle -7500403 true true 70 85 38
Circle -7500403 true true 130 25 38
Circle -7500403 true true 96 51 108
Circle -16777216 true false 113 68 74
Polygon -10899396 true false 189 233 219 188 249 173 279 188 234 218
Polygon -10899396 true false 180 255 150 210 105 210 75 240 135 240

house
false
0
Rectangle -7500403 true true 45 120 255 285
Rectangle -16777216 true false 120 210 180 285
Polygon -7500403 true true 15 120 150 15 285 120
Line -16777216 false 30 120 270 120

leaf
false
0
Polygon -7500403 true true 150 210 135 195 120 210 60 210 30 195 60 180 60 165 15 135 30 120 15 105 40 104 45 90 60 90 90 105 105 120 120 120 105 60 120 60 135 30 150 15 165 30 180 60 195 60 180 120 195 120 210 105 240 90 255 90 263 104 285 105 270 120 285 135 240 165 240 180 270 195 240 210 180 210 165 195
Polygon -7500403 true true 135 195 135 240 120 255 105 255 105 285 135 285 165 240 165 195

line
true
0
Line -7500403 true 150 0 150 300

line half
true
0
Line -7500403 true 150 0 150 150

pentagon
false
0
Polygon -7500403 true true 150 15 15 120 60 285 240 285 285 120

person
false
0
Circle -7500403 true true 110 5 80
Polygon -7500403 true true 105 90 120 195 90 285 105 300 135 300 150 225 165 300 195 300 210 285 180 195 195 90
Rectangle -7500403 true true 127 79 172 94
Polygon -7500403 true true 195 90 240 150 225 180 165 105
Polygon -7500403 true true 105 90 60 150 75 180 135 105

plant
false
0
Rectangle -7500403 true true 135 90 165 300
Polygon -7500403 true true 135 255 90 210 45 195 75 255 135 285
Polygon -7500403 true true 165 255 210 210 255 195 225 255 165 285
Polygon -7500403 true true 135 180 90 135 45 120 75 180 135 210
Polygon -7500403 true true 165 180 165 210 225 180 255 120 210 135
Polygon -7500403 true true 135 105 90 60 45 45 75 105 135 135
Polygon -7500403 true true 165 105 165 135 225 105 255 45 210 60
Polygon -7500403 true true 135 90 120 45 150 15 180 45 165 90

square
false
0
Rectangle -7500403 true true 30 30 270 270

square 2
false
0
Rectangle -7500403 true true 30 30 270 270
Rectangle -16777216 true false 60 60 240 240

star
false
0
Polygon -7500403 true true 151 1 185 108 298 108 207 175 242 282 151 216 59 282 94 175 3 108 116 108

target
false
0
Circle -7500403 true true 0 0 300
Circle -16777216 true false 30 30 240
Circle -7500403 true true 60 60 180
Circle -16777216 true false 90 90 120
Circle -7500403 true true 120 120 60

tree
false
0
Circle -7500403 true true 118 3 94
Rectangle -6459832 true false 120 195 180 300
Circle -7500403 true true 65 21 108
Circle -7500403 true true 116 41 127
Circle -7500403 true true 45 90 120
Circle -7500403 true true 104 74 152

triangle
false
0
Polygon -7500403 true true 150 30 15 255 285 255

triangle 2
false
0
Polygon -7500403 true true 150 30 15 255 285 255
Polygon -16777216 true false 151 99 225 223 75 224

truck
false
0
Rectangle -7500403 true true 4 45 195 187
Polygon -7500403 true true 296 193 296 150 259 134 244 104 208 104 207 194
Rectangle -1 true false 195 60 195 105
Polygon -16777216 true false 238 112 252 141 219 141 218 112
Circle -16777216 true false 234 174 42
Rectangle -7500403 true true 181 185 214 194
Circle -16777216 true false 144 174 42
Circle -16777216 true false 24 174 42
Circle -7500403 false true 24 174 42
Circle -7500403 false true 144 174 42
Circle -7500403 false true 234 174 42

turtle
true
0
Polygon -10899396 true false 215 204 240 233 246 254 228 266 215 252 193 210
Polygon -10899396 true false 195 90 225 75 245 75 260 89 269 108 261 124 240 105 225 105 210 105
Polygon -10899396 true false 105 90 75 75 55 75 40 89 31 108 39 124 60 105 75 105 90 105
Polygon -10899396 true false 132 85 134 64 107 51 108 17 150 2 192 18 192 52 169 65 172 87
Polygon -10899396 true false 85 204 60 233 54 254 72 266 85 252 107 210
Polygon -7500403 true true 119 75 179 75 209 101 224 135 220 225 175 261 128 261 81 224 74 135 88 99

wheel
false
0
Circle -7500403 true true 3 3 294
Circle -16777216 true false 30 30 240
Line -7500403 true 150 285 150 15
Line -7500403 true 15 150 285 150
Circle -7500403 true true 120 120 60
Line -7500403 true 216 40 79 269
Line -7500403 true 40 84 269 221
Line -7500403 true 40 216 269 79
Line -7500403 true 84 40 221 269

x
false
0
Polygon -7500403 true true 270 75 225 30 30 225 75 270
Polygon -7500403 true true 30 75 75 30 270 225 225 270
@#$#@#$#@
NetLogo 6.1.1
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
@#$#@#$#@
default
0.0
-0.2 0 0.0 1.0
0.0 1 1.0 0.0
0.2 0 0.0 1.0
link direction
true
0
Line -7500403 true 150 150 90 180
Line -7500403 true 150 150 210 180
@#$#@#$#@
0
@#$#@#$#@
